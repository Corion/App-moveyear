#!perl
use strict;
use warnings;
use Getopt::Long;
use File::Copy 'mv';
use File::Basename;
use File::Path 'make_path';
use File::Spec;

=head1 NAME

move-year - Move files to directories according to a date in their filename

=cut

my $date_type;
GetOptions(
    'dry-run|n'            => \my $dry_run,
    'verbose'              => \my $verbose,

    'date-regex|d=s'       => \my $date_regex,
    'date-regex-order|o=s' => \my $date_regex_order,
    'date-type|t=s'        => \$date_type,
    'ymd'                  => sub { $date_type = 'ymd' },
    'dmy'                  => sub { $date_type = 'dmy' },
    'ym'                   => sub { $date_type = 'ym' },
    'my'                   => sub { $date_type = 'my' },
    'y'                    => sub { $date_type = 'y' },
    'force|f'              => \my $force,
    'create'               => \my $create_directories,
    'directory-style|s=s'  => \my $directory_style,
    'part-separator'       => \my $part_separator,

    'i'                    => \my $dont_overwrite,

    'strict'               => \my $strict,
);

$verbose //= $dry_run;

$part_separator //= '-';
$directory_style //= 'y';

$date_type //= 'ymd';

my %date_type = (
    ymd => qr/(?<year>(?:20)\d\d)([-]?)(?<month>0\d|1[012])(\2)(?<day>[012]\d|3[01])/,
    dmy => qr/(?<day>[012]\d|3[01])([-]?)(?<month>0\d|1[012])(\2)(?<year>(?:20)\d\d)/,
    ym  => qr/(?<year>(?:20)\d\d)([-]?)(?<month>0\d|1[012])/,
    my  => qr/(?<month>0\d|1[012])([-]?)(?<year>(?:20)\d\d)/,
    y   => qr/(?<year>(?:20)\d\d)/,
);

$date_regex //= $date_type{ $date_type };

my $target = pop @ARGV;

if( ! -d $target ) {
    die "'$target' is not a directory";
}

# Should we also support hour, minute, second?!
my %longname = (
    'y' => 'year',
    'm' => 'month',
    'd' => 'day',
);

sub find_ymd {
    my( $source ) = @_;
    use Regexp::Debugger;
    my $name = basename $source;
    if( $name!~ /$date_regex/ ) {
        return;
    }

    my %ymd;
    if( keys %- ) { # we have named captures
        for (keys %longname) {
            $ymd{ $longname{ $_ } } //= $+{ $longname{ $_ }} // $+{ $_ };
        }
    } else {
        my @ymd = split //, $date_regex_order;
        for my $i (0..$#ymd) {
            $ymd{ $longname{ $ymd[$i] }} //= substr( $source, $-[$i+1], $+[$i+1] );
        }
    };

    $ymd{ year } += 2000 if $ymd{ year } < 100;
    for my $n ( values %longname ) {
        if( defined $ymd{ $n }) {
            $ymd{ $n } = sprintf '%02d', $ymd{ $n };
        }
    };

    return %ymd;
}

sub move_file {
    my( $source,$target,%ymd ) = @_;

    my $target_dir = File::Spec->catdir( $target,
                        map { $ymd{ $_ } }
                        map { $longname{ $_ } }
                        split //, $directory_style
    );
    if( $create_directories && ! -d $target_dir ) {
        if( $verbose ) {
            print "Creating '$target_dir'\n";
        }
        if( ! $dry_run ) {
            make_path $target_dir;
        };
    }
    my $name = basename($source);
    my $target_file = "$target_dir/$name";
    if(($dry_run || -d $target_dir) && (! -e $target_file || $force)) {
        if( $verbose ) {
            print "Moving '$source' to '$target_file'\n";
        }
        if( ! $dry_run ) {
            mv $source => $target_file;
        };
    }
}

for my $file (@ARGV) {
    my %ymd = find_ymd( $file );
    if( keys %ymd ) {
        move_file( $file, $target, %ymd )
    } elsif( $strict ) {
        die "Could not find a date in '$file'\n";
    } else {
        warn "Skipping '$file' (no date found)\n";
    }
}
